= 示例：删除某些XML属性
:keywords: studio, anypoint, transform, transformer, format, aggregate, rename, split, filter convert, xml, json, csv, pojo, java object, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping




您可能希望从已知包含敏感数据的XML标记中删除特定属性。该示例在DataWeave头中定义了一个函数，用于从传递的元素及其子元素中删除特定的XML属性。



这个例子使用：

*  `mapObject`遍历每个元素。
*  `@`引用XML元素中的属性。
*  `if`和`else`有条件地仅在XML属性存在时列出它们，并且只在子元素是对象时才递归调用该函数。
*  `is`来检查每个孩子的类型是否为`Object`。
*  `-`从XML属性列表中移除一个元素。
*  `inlineCloseOn="empty"`，以便输出XML在没有值时显示自闭标签。




.DataWeave
[source,dataweave, linenums]
----
%dw 2.0
input payload application/json
output application/xml inlineCloseOn="empty"
var removeAttribute = (element,attrName) ->
  element mapObject (value, key) -> {
    (key) @(
        (
          if (key.@?)
            (key.@ - attrName)
          else {}
        )
    ) :
    if (value is Object)
      removeAttribute(value, attrName)
    else value
  }
---
removeAttribute(payload, "password")
----

。输入
[source,xml, linenums]
----
<users>
  <user username="Julian" password="1234"/>
  <user username="Mariano" password="4321"/>
</users>
----

.OUTPUT
[source,xml, linenums]
----
<?xml version='1.0' encoding='US-ASCII'?>
<users>
  <user username="Julian"/>
  <user username="Mariano"/>
</users>
----

== 相关示例


*  link:dataweave-cookbook-output-self-closing-xml-tags[输出自我关闭的XML标签]

*  link:dataweave-cookbook-insert-attribute[将属性插入到XML标签中]

*  link:dataweave-cookbook-include-xml-namespaces[包含XML名称空间]

*  link:dataweave-cookbook-exclude-field[从输出中排除字段]

*  link:dataweave-cookbook-output-a-field-when-present[出现时输出字段]

*  link:dataweave-cookbook-conditional-list-reduction-via-function[通过函数有条件地减少列表]


== 另请参阅


// * link:dw-functions-core[DataWeave核心功能]

*  link:dataweave-selectors[DataWeave选择器]

*  link:dataweave-cookbook[DataWeave食谱]
