=  DataWeave类型
:keywords: studio, anypoint, esb, transform, transformer, format, aggregate, rename, split, filter convert, xml, json, csv, pojo, java object, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

// TODO：需要将此信息与核心类型合并。讨论MDA。
DataWeave支持多种元素的数据类型，包括数组，布尔值，对象，日期，时间以及此处描述的其他许多数据类型。请注意，有关这些类型的更多详细信息可在 link:dw-core-types[核心类型（dw :: Core）]中找到。

== 任何

`Any`是顶级类型。 `Any`扩展了所有的系统类型。这意味着可以将任何内容分配给`Any`类型的变量。

//。定义

== 阵列

数组是一系列元素。它需要一个类型（T）来表示列表中的元素。例如：`Array<Number>``表示一个数字数组。
// TODO：需要更多信息和可能的示例，它显示了我们正在讨论的类型（T）

。例
[source,DataWeave,linenums]
----
%dw 2.0
output application/json
var x = "words"
---
[ "My", "three", x ]
----

=== 条件元素

根据条件，数组可以定义出现或不出现的元素。

条件元素的形式为`（value）if condition

。转变
[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
[(1) if true, (2) if false]
----

.OUTPUT
[source,json,linenums]
----
[1]
----

[[dw_type_binary]]
== 二进制

`Binary`是一个blob的类型。

[[dw_type_boolean]]
== 布尔

`Boolean`由关键字`true`和`false`定义。

[[dw_type_cdata]]
==  CData的

XML定义了一个名为`CData`的自定义类型，它继承并扩展了`String`。它用于识别CDATA XML块。它可以用来告诉编写者将内容包装到CDATA中，或者检查输入字符串是否到达CDATA块内。

。转变
[source,DataWeave, linenums]
----
%dw 2.0
output application/xml
---
{
  users:
  {
    user : "Mariano" as CData,
    age : 31 as CData
  }
}
----

.OUTPUT
[source,xml,linenums]
----
<?xml version="1.0" encoding="UTF-8"?>
<users>
  <user><![CDATA[Mariano]]></user>
  <age><![CDATA[31]]></age>
</users>
----

//。定义

[source]
----
String {cdata: true}
----

[[dw_type_comparable]]
== 可比

`Comparable`是一种联合类型，表示可以相互比较的所有类型。

//。定义

[source]
----
String | Number | Boolean | DateTime | LocalDateTime | LocalTime | Time | TimeZone
----

[[dw_type_dates]]
== 日期和时间

DataWeave中的日期遵循 link:https://docs.oracle.com/javase/8/docs/api/java/time/format/DateTimeFormatter.html[ISO-8601标准]，文字在`|`个字符之间定义。

该语言具有以下本机日期类型：

* `日期
* `日期时间
* `LocalDateTime
* `本地时间
* `期
* `时间
* `时区

[[dw_type_dates_date]]
=== 日期

由`Year`，`Month`和`Day`表示的`Date`，指定为`|YYYY-MM-dd|`。 `Date`类型没有时间分量。

。例
[source,DataWeave, linenums]
-----------------------------------------------------------------
|2003-10-01|
-----------------------------------------------------------------

[[dw_type_dates_datetime]]
=== 日期时间

时区内的日期和时间。它是`Date` + `Time` + `TimeZone`的连接点。

。例
[source,DataWeave, linenums]
-----------------------------------------------------------------
|2003-10-01T23:57:59-03:00|
-----------------------------------------------------------------

===  LocalDateTime

当前`TimeZone`中的`DateTime`。

。例
[source,DataWeave, linenums]
-----------------------------------------------------------------
|2003-10-01T23:57:59|
-----------------------------------------------------------------

[[dw_type_dates_localtime]]
=== 本地时间

当前`TimeZone`中的`Time`。

[[dw_type_dates_period]]
=== 期

期间的形式如下：

- `P [n]的Y [n]的M [n]的DT [N] H [N] M [Ñ] S
- `p <date> T <time>

其中[n]由[n]后面的每个日期和时间元素的值替换。

`P`是放置在持续时间表示开始处的持续时间指示符。

-  `Y`是年份代号（例如`|P1Y|`）
-  `M`是月份标识符（例如`|P1M|`）
-  `D`是指定日期（例如`|P1D|`）

`T`是表示时间组件之前的时间指示符。

-  `H`是小时代号（例如`|PT1H|`）
-  `M`是分号（例如`|PT1M|`）
-  `S`是第二个指定符（例如`|PT1S|`）

。转变
[source,DataWeave, linenums]
----------------------------------------------------------------
%dw 2.0
output application/json
---
a: |20:00:00| + |PT1M1S|
----------------------------------------------------------------

.OUTPUT
[source,json,linenums]
----------------------------------------------------------------
{
  "a": "20:01:01"
}
----------------------------------------------------------------

[[dw_type_dates_time]]
=== 时间

特定`TimeZone`中的时间，指定为`|HH:mm:ss.SSS|`。

。例
[source,DataWeave, linenums]
-----------------------------------------------------------------
|23:59:56|
-----------------------------------------------------------------

[[dw_type_dates_timezone]]
=== 时区

与{格林威治标准时间（格林威治标准时间）相关的`Time`。 `TimeZone`必须包含`+`或`-`。例如，`|03:00|`是一个时间，而`|+03:00|`是一个`TimeZone`。

。例
[source,DataWeave, linenums]
----
|-08:00|
----

=== 日期分解

为了访问日期的不同部分，必须使用特殊的选择器。

。转变
[source,DataWeave, linenums]
-----------------------------------------------------------------
%dw 2.0
output application/json
var myDate = |2003-10-01T23:57:59.700-03:00|
---
{
  year: myDate.year,
  month: myDate.month,
  day: myDate.day,
  hour: myDate.hour,
  minutes: myDate.minutes,
  seconds: myDate.seconds,
  milliseconds: myDate.milliseconds,
  nanoseconds: myDate.nanoseconds,
  quarter: myDate.quarter,
  dayOfWeek: myDate.dayOfWeek,
  dayOfYear: myDate.dayOfYear,
  offsetSeconds: myDate.offsetSeconds
}
-----------------------------------------------------------------

.OUTPUT
[source,json,linenums]
-----------------------------------------------------------------
{
  "year": 2003,
  "month": 10,
  "day": 1,
  "hour": 23,
  "minutes": 57,
  "seconds": 59,
  "milliseconds": 700,
  "nanoseconds": 700000000,
  "quarter": 4,
  "dayOfWeek": 3,
  "dayOfYear": 274,
  "offsetSeconds": -10800
}
-----------------------------------------------------------------

=== 更改日期的格式

您可以通过以下方式使用*as*指定日期以您喜欢的任何格式：

。转变
[source,DataWeave, linenums]
----------------------------------------------------------------
%dw 2.0
output application/json
---
formattedDate: |2003-10-01T23:57:59| as String {format: "YYYY-MM-dd"}
----------------------------------------------------------------

.OUTPUT
[source,json,linenums]
-----------------------------------------------------------------
{
  "formattedDate": "2003-10-01"
}
-----------------------------------------------------------------

如果您在变换中进行了多个类似的转换，您可能希望将自定义类型定义为标题中的指令，并将每个日期设置为该类型的日期。

。转变
[source,DataWeave, linenums]
----------------------------------------------------------------
%dw 2.0
output application/json
type Mydate = String { format: "YYYY/MM/dd" }
---
{
  formattedDate1: |2003-10-01T23:57:59| as Mydate,
  formattedDate2: |2015-07-06T08:53:15| as Mydate
}
----------------------------------------------------------------

.OUTPUT
[source,json,linenums]
-----------------------------------------------------------------
{
  "formattedDate1": "2003/10/01",
  "formattedDate2": "2015/07/06"
}
-----------------------------------------------------------------

请注意，类型名称区分大小写。

[[dw_type_dictionary]]
== 字典
泛型词典界面。

//。定义
//问题：内部？
////
[source]
----
{ _?: T }
----
////

[[dw_type_enum]]
== 枚举

此类型基于 link:https://docs.oracle.com/javase/7/docs/api/java/lang/Enum.html[枚举Java类]。
它必须始终与`class`属性一起使用，并指定类的完整Java类名称，如本例中所示。

。转变
[source,DataWeave, linenums]
----
%dw 2.0
output application/java
---
"Male" as Enum {class: "com.acme.GenderEnum"}
----

//。定义
////
[source]
----
String {enumeration: true}
----
////

[[dw_type_iterator]]
== 迭代

`Iterator`类型基于遍历数组的 link:https://docs.oracle.com/javase/8/docs/api/java/util/Iterator.html[迭代器Java类]。 `Iterator`包含一个集合，并包含迭代和过滤的方法。

请注意，就像Java类一样，迭代器只能被使用一次。例如，如果您将此值传递给记录器，则会导致消耗该记录器，并且流程中的其他元素不再可读。

[[dw_type_key]]
== 关键

`Object`的{​​{0}}类型。另请参阅`Object`和`Value`类型。

//。定义

[[dw_type_namespace]]
== 命名空间

一个`Namespace`类型，由一个URI和一个前缀表示。

//。定义

[[dw_type_nothing]]
== 没什么

`Nothing`类型是底层类型。它可以分配给所有类型。
//。定义

[[dw_type_null]]
== 空

`Null`类型。
// 去做

[[dw_type_number]]
== 数

只有一种数字类型支持浮点数和整数。在任何操作中都没有精度损失，引擎始终以最高性能的方式存储数据，不会影响精度。

[[dw_type_object]]
== 对象

将任何对象表示为`key:value`对的集合。

。例

[source,DataWeave,linenums]
---------------------------------------------------------
%dw 2.0
output application/json
---
{
  name: "Annie"
}
---------------------------------------------------------

=== 单值对象

如果一个对象只有一对`key:value`，则不需要括号`{ }`：

。例

[source,DataWeave,linenums]
---------------------------------------------------------
%dw 2.0
output application/json
---
name: "Annie"
---------------------------------------------------------

=== 条件元素

对象可以基于条件表达式定义条件*key*：*value*对。
条件元素的形式为`（key：value）if condition

[source,DataWeave,linenums]
---------------------------------------------------------
%dw 2.0
output application/xml
---
file: {
  name: "transform",
  (extension: "zip") if payload.fileSystem?
}
---------------------------------------------------------

仅当fileSystem属性存在于有效内容中时（此字段可能包含任何值，而不仅仅是`true`），此示例才会输出名为"extension"的附加字段。

[source,xml,linenums]
--------------------------------------------------------
<?xml version="1.0" encoding="UTF-8"?>
<file>
  <name>transform</name>
  <extension>zip</extension>
</file>
--------------------------------------------------------

如果缺席：

[source,xml,linenums]
--------------------------------------------------------
<?xml version="1.0" encoding="UTF-8"?>
<file>
  <name>transform</name>
</file>
--------------------------------------------------------

=== 动态密钥

为了通过表达式指定一个键，表达式应该用圆括号包装。

。转变
[source,DataWeave,linenums]
----
%dw 2.0
output application/json
var dynamicKey = "language"
---
{
  (dynamicKey): "Data Weave"
}
----

.OUTPUT
[source,json,linenums]
----
{
  "language": "Data Weave"
}
----

=== 动态元素

动态元素允许您将表达式的结果添加为对象的`key:value`对。该表达式必须是`object`或`array of objects`。

。转变
[source,DataWeave,linenums]
--------------------------------------------------------
%dw 2.0
output application/json
var x = [
  {b: "b"},
  {c: "c", d: "d"}
],
var y = {e: "e"}
---
{
  a: "a",
  (x),
  (y)
}
--------------------------------------------------------

值得注意的是_parentheses_（例如`(x)`（其中`x`是头中显示的变量）之间的表达式应该返回一个对象数组。该数组中的所有对象都合并在一起。它们也与包含的对象合并。所以输出如下所示：

.OUTPUT
[source,json,linenums]
--------------------------------------------------------
{
  "a": "a",
  "b": "b",
  "c": "c",
  "d": "d",
  "e": "e"
}
--------------------------------------------------------

=== 条件XML属性

您可能希望输出仅包含基于条件的某些XML属性。
条件元素的形式为`（key：value）if condition

。转变
[source,DataWeave,linenums]
----
%dw 2.0
output application/xml
---
{
  name @(
    (company: "Acme") if false,
    (transform: "Anything") if true
  ): "DataWeave"
}
----

.OUTPUT
[source,xml,linenums]
----
<?xml version='1.0' encoding='US-ASCII'?>
<name transform="Anything">DataWeave</name>
----

=== 动态XML属性

您可能希望在特定位置包含一组不断变化的键：值对作为XML属性。

。输入
[source,json,linenums]
----
{
  "company": "Mule",
  "product": "DataWeave"
}
----

。转变
[source,dataweave,linenums]
----
%dw 2.0
output application/xml
---
transformation @((payload)): "Transform from anything to anything"
----

.OUTPUT
[source,xml,linenums]
----
<?xml version='1.0' encoding='US-ASCII'?>
<transformation company="Mule" product="DataWeave">Transform from anything to anything</transformation>
----

[[dw_type_range]]
== 范围

范围类型表示一系列数字。

//。定义
////
[source]
----
Range
----
////

[[dw_type_regex]]
== 的正则表达式

正则表达式定义在`/`之间。例如`/\d+/`表示0-9之间的多个数字。这些可以用作对字符串起作用的特定操作中的参数，比如匹配或替换，或者作用于对象和数组（如过滤器）的操作。

[[dw_type_simpletype]]
==  SimpleType的

代表所有简单类型的联合类型。

//。定义

[source]
----
String | Boolean | Number | DateTime | LocalDateTime | Date | LocalTime | Time | TimeZone | Period
----

[[dw_type_string]]
== 字符串

一个字符串可以通过使用双引号或单引号来定义。

[source,DataWeave, linenums]
--------------------------------------------------------
{
  doubleQuoted: "Hello",
  singleQuoted: 'Hello',
}
--------------------------------------------------------

=== 字符串插值

字符串插值允许您直接在字符串中嵌入变量或表达式。

。转变
[source,DataWeave, linenums]
--------------------------------------------------------
%dw 2.0
output application/json
var name = "Shoki"
---
{
    Greeting: "Hi, my name is $name",
    Sum: "1 + 1 = $(1 + 1)"
}
--------------------------------------------------------

.OUTPUT
[source,json,linenums]
--------------------------------------------------------
{
  "Greeting": "Hi, my name is Shoki",
  "Sum": "1 + 1 = 2"
}
--------------------------------------------------------

[[dw_type_tryresult]]
==  TryResult

评估委托并返回包含结果或错误消息的对象。请参阅`try`示例。成功的`TryResult`包含`result`字段和`true`的`success`值。不成功的`TryResult`包含`error`字段和`false`的`success`值。

。定义

[source,Dataweave, linenums]
----
{
  success: Boolean,
  result?: T,
  error?: {
    kind: String,
    message: String,
    stack?: Array<String>,
    location?: String
  }
}
----

[[dw_type_type]]
== 输入

表示DataWeave类型系统中的数据类型。

// 去做。

[[dw_type_uri]]
== 乌里

指定为`Uri`的URI。

//。定义
////
[source]
----
Uri

----
////

== 另请参阅

*  link:dataweave-language-introduction[DataWeave语言介绍]
// * link:dw-functions-core[DataWeave核心功能]
*  https://www.anypoint.mulesoft.com/exchange/?search=dataweave [Anypoint Exchange（使用DataWeave的项目列表）]

////
==
表达式也可能很复杂，也就是说它们可以由其他表达式组成。这可以通过在数组或对象中嵌套表达式或通过使用运算符来实现。
在复杂表达式中，一个表达式的结果为其他表达式的后续执行设置上下文。您只需要记住每个表达式都会生成一个Object，一个Array或一个Variable。当您理解这些数据类型的结构是从DataWeave表达式构建的时，您将有效地理解如何使用DataWeave。





=== 简单值

简单值可以是以下类型：

*  link:dataweave-types#string[*串*]：双引号（"Hello"）或单引号（'Hello'）
*  link:dataweave-types#boolean[*布尔*]：文字是真或假
*  link:dataweave-types#number[*数*]：支持十进制和整数值（例如2.0）
*  link:dataweave-types#dates[*日期*]：由"|"包围的IS0-8601（例如：| 2003-10-01T23：57：59Z |）
*  link:dataweave-types#regular-expressions[*正则表达式*]：由"/"（例如：/（\ d +） - （\ d +）/）括起的正则表达式

=== 阵列

数组表示为一系列值表达式。

。输入
[source, dataweave]
--------------------------------------------------------------------
[ 1, 2 + 2, 3 * 3, $x ]
--------------------------------------------------------------------

。转变
[source, dataweave, linenums]
----
%dw 2.0
output application/json
---
[ "My", "three", "words" ]
----

有关数组的更多详细信息，请参阅 link:dataweave-types#array[DataWeave类型]。

=== 对象

这些表示为逗号分隔的由大括号{}围绕的键：值对序列。

。转变
[source, dataweave, linenums]
---------------------------------------------------------------------
%dw 2.0
output application/xml
---
myoutput:{
  name : "Jill",
  payload : payload.id + 3
  }
---------------------------------------------------------------------

.OUTPUT
[source,xml,linenums]
---------------------------------------------------------------------
<?xml version="1.0" encoding="UTF-8"?>
<myoutput>
  <name>Jill</name>
  <payload>5</payload>
</myoutput>
---------------------------------------------------------------------

请注意，键和值都可以是*expressions*。

有关对象的更多详细信息，请参阅 link:dataweave-types#object[DataWeave类型]。


== 变量

=== 常量

在DataWeave头文件中，将常量定义为指令，然后可以在变换主体的任何部分将这些变量引用为变量，就像您对输入变量所做的一样。
以下内容创建一个XML文档并在输出语言元素中插入语言"Español"的常量值。



=== 作用域变量

变量标题中声明的变量总是具有全局范围，声明和初始化变量的范围是有限的，你可以在变换体的任何部分这样做。

您可以使用文字表达式，变量引用表达式或函数表达式来初始化这些变量。他们可以在其初始化中引用任何其他范围变量或任何输入变量或常量。声明和初始化可以作为任何文字表达式的前缀，但是您必须意识到它们预先限定的范围。你不能在其范围之外引用一个变量。

要在DataWeave正文中声明变量，支持以下语法：*using (<variable-name> = <expression>)*并且必须在定义其中存在的文字内容之前写入该语法。
要引用一个已经初始化的变量，你可以用你为其定义的名称来调用它，就像其他变量一样，或者你也可以用*$<variable-name>*的形式写出它。

考虑下面的例子：

*Scoped to Simple Value*

[source, dataweave, linenums]
----
%dw 2.0
output application/json
---
using (x = 2) 3 + x # <1>
----
<1>结果仅为5

*Scoped to Array literal*

[source, dataweave, linenums]
----
%dw 2.0
output application/json
---
using (x = 2) [1, x, 3]
----

*Scoped to Object literal*

[source, dataweave, linenums]
----
%dw 2.0
output application/xml
---
{
  person: using (user = "Greg", gender = "male") { # <1>
    name: user, # <2>
    gender: gender
  }
}
----

<1>声明和初始化。
<2> *user*是一个有效的引用，因为它位于为其声明的对象*person*内。

*Invalid Reference outside of Scope*

[source, dataweave, linenums]
----
%dw 2.0
output application/xml
---
entry: using (firstName = "Annie", lastName = "Point") {
  person: using (user = firstName, gender = "male") {
    name: user,
    gender: gender
  },
  sn: lastName, # <1>
  gen: gender # <2>
}
----

<1>参考*lastName*是有效的，因为它在范围内。
<2>参考*gender*无效，因为性别是在*person*对象中声明的，并且此引用存在于该对象的范围之外。

== 表达式

DataWeave允许您使用表达式值将逻辑放入您的脚本中。
DataWeave中的所有表达式都会返回一个值，这些可以分为：

*  <<Operators>>
*  <<Selectors>>
*  <<Flow Control Expressions>>
////

////
=  DataWeave类型：
== 字符串
== 布尔
== 数
== 范围
== 命名空间
== 乌里
== 日期时间
==  LocalDateTime
== 日期
== 本地时间
== 时间
== 时区
== 期
== 二进制
== 空
== 的正则表达式
== 没什么
== 任何
== 阵列
== 对象
== 输入

== TryResult
////
