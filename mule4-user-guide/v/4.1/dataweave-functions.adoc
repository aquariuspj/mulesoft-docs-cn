关于DataWeave函数= 
DataWeave可以像其他编程语言（如Java或JavaScript）一样定义函数。

您可以使用标题部分中的`fun`声明来定义函数。像其他编程语言一样，DataWeave函数允许您将DataWeave表达式重构为可重用的代码块。在DataWeave中，每个代码块都是一个表达式，当它从DataWeave正文表达式调用时，该表达式会立即进行评估。函数可以定义和使用一个或多个参数。

这是一个简单的DataWeave函数，它接受一个String参数：

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
fun toUpper(aString) = upper(aString)
---
toUpper("hello") //results in "HELLO"
----

DataWeave函数的参数可以是任何DataWeave表达式。该函数产生与前一个例子相同的结果：

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
fun toUpper(aString) = upper(aString)
---
toUpper("h" ++ "el" ++ lower("LO") ) //results in "HELLO"
----

因为任何DataWeave表达式都可以传递给函数，所以在使用表达式之前，可能需要测试传递给函数的参数的类型。匹配运算符可以对此有所帮助：

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
fun toUpper(aString)
= aString match {
  case is String -> upper(aString)
  else -> null
}
---
toUpper("h" ++ "el" ++ lower("LO") ) //results in "HELLO"
----

此示例创建一个函数，将数字字符串重新格式化为常用的电话号码格式：

。示例：toPhoneFormat（）函数
[source,DataWeave, linenums]
----
fun toPhoneFormat(str: String) =
    "(" ++ str[0 to 2] ++ ") " ++ str[3 to 5] ++ "-" ++ str[6 to 9]
---
toPhoneFormat("1234567890")
----

.ToPhoneFormat的输出（"1234567890"）
----
"(123) 456-7890"
----

== 另请参阅

link:dw-functions[DataWeave函数参考]
